-- upgrade_offload_repo_deltas.sql
--
-- LICENSE_TEXT
--

set termout off feedback off serveroutput on
spool sql/upgrade_offload_repo_deltas.tmp replace
declare

    v_current_version varchar2(30);

    type numbers_ntt is table of number;

    function to_numbers(v_version in varchar2) return numbers_ntt is
        v_numbers numbers_ntt := numbers_ntt();
    begin
        v_numbers.extend(2);
        v_numbers(1) := to_number(regexp_substr(v_version, '^[0-9]+'));
        v_numbers(2) := to_number(regexp_substr(v_version, '[0-9]+\.[0-9]+$'));
        return v_numbers;
    end to_numbers;

    function less_than(p_current_version in varchar2, p_target_version in varchar2) return boolean is
        v_current_version numbers_ntt;
        v_target_version numbers_ntt;
    begin
        v_current_version := to_numbers(p_current_version);
        v_target_version  := to_numbers(p_target_version);
        return (v_current_version(1) < v_target_version(1)) or (v_current_version(1) = v_target_version(1) and v_current_version(2) < v_target_version(2));
    end less_than;

    procedure check_version ( p_current_version in varchar2, p_target_version in varchar2 ) is
    begin
        if less_than(p_current_version, p_target_version) then
            dbms_output.put_line('@@create_offload_repo_' || replace(p_target_version, '.') || '.sql');
        end if;
    end check_version;

begin

    dbms_output.put_line(q'{-- Script generated by upgrade_offload_repo_deltas.sql}');
    dbms_output.put_line(q'{-- LICENSE_TEXT}');
    dbms_output.put_line(q'{--}');

    select nvl(max(version), '0.0.0')
    into   v_current_version
    from   goe_version
    where  latest = 'Y';

    -- Follow this pattern for each repo version file in sequence...
    check_version(v_current_version, '1.0.0');

end;
/
spool off
set termout on feedback on serveroutput off

@@upgrade_offload_repo_deltas.tmp
